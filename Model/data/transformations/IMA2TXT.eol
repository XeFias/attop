operation buildMAText(printDot:Boolean) : Sequence(Integer, Integer, String) {

	var IMA = IMA!InterimAutomaton.allInstances().first();
	var disruptions = IMA!Disruption.allInstances();
	var states = IMA!State.allInstances();
	var totNrOfStates = 0;
	var totNrOfTransitions = 0;
	var probStatesId = states.size() * 2 - 1;
	var probabilityValues:OrderedSet(IMA!StringToDoubleMap)	= IMA.probabilities;
	var rateValues:OrderedSet(IMA!StringToDoubleMap)		= IMA.rates;

	var maStr:String = "";
	for (state in states) {
		if( state.outgoing.size() > 0 ){
			var beIDs = state.outgoing.collect( i|i.beId ).asSet();
			for ( beID in beIDs ) { /* handel one outgoing Bes at a time */
				if( beID <> "SUCCESS" and beID <> "FAIL" ){
					maStr = maStr.concat( toMarkoveTransitionString( printDot, state.id, probStatesId++, beID, rateValues.get(beID) ) );
					totNrOfTransitions++;
					maStr = maStr.concat( toDecisionTansactionString( printDot, probStatesId, probStatesId++, beID ) );
					totNrOfTransitions++; 
					for ( disruptions in state.outgoing.select( i | i.beId == beID ) ){
						var probabilityDivider = disruptions.size(); probabilityDivider.println("probabilityDivider = ");
						for ( disruption in disruptions.select( i | i.newStatus == IMA!StatusType#SUCCESS ) ) {
							maStr = maStr.concat( toProbabilityTransitionString( printDot, probStatesId, disruption.target.id, beID, IMA!StatusType#SUCCESS, probabilityValues.get(beID) / probabilityDivider) );
							totNrOfTransitions++;
						}
						for ( disruption in disruptions.select( i | i.newStatus == IMA!StatusType#FAILED ) ) {
							maStr = maStr.concat( toProbabilityTransitionString( printDot, probStatesId, disruption.target.id, beID, IMA!StatusType#FAILED, ((1 - probabilityValues.get(beID)) / probabilityDivider) ) );
							totNrOfTransitions++;
						}
					}
				} else { /* end for !SUCCES and !FAIL */
					for ( disruptions in state.outgoing.select( i | i.beId == beID ) ){
						for ( disruption in disruptions ) {
							maStr = maStr.concat( toFinalTransitionString( printDot, state.id, disruption.target.id, beID ) );
							totNrOfTransitions++;
						}
					}
				}
			}
		}
	}
	return Sequence{ probStatesId + 1, totNrOfTransitions, maStr };
}

operation toMarkoveTransitionString(toDot:Boolean, stateID:Integer, nextProbabilityState:Integer, beID:String, value:Real) : String {
	if (toDot) {
		return (stateID + " -> " + nextProbabilityState + " [label = \"!" + beID + "\\n rate = " + value + "\", style=dashed]\n");
	} else {
		return ("("+ stateID + ", \"rate " + value + "\", " + nextProbabilityState + ")\n" );
	}
}

operation toDecisionTansactionString(toDot:Boolean, stateID:Integer, nextProbabilityState:Integer, beID:String) : String {
	if (toDot) {
		return ( (stateID + " -> " + nextProbabilityState + "\n") ).concat( nextProbabilityState + " [shape=point];\n" );
	} else {
		return ( "(" + stateID + ", \"" + beID + "\", 1)\n" );	/*TODO select target state */
	}
}

operation toProbabilityTransitionString(toDot:Boolean, dicisionStateID:Integer, nextState:Integer, beID:String, negation, value:Real) : String {
	var negationStr = "";
	if (negation == IMA!StatusType#FAILED) { negationStr = "Â¬";}
	if (toDot) {
		return (dicisionStateID + " -> " + nextState + " [label = \"" + negationStr + beID + "\\n prob = " + value.format("%.4s") + "\"]\n");
	} else {
		return ( "(" + dicisionStateID + ", \"prob " + value + "\", " + nextState + ")\n" );
	}
}

operation toFinalTransitionString(toDot:Boolean, stateID:Integer, nextState:Integer, beID:String) : String {
	if (toDot) {
		return ( (stateID + " -> " + nextState + " [label = \"" + beID + "\"]\n") );
	} else {
		return ( "(" + stateID + ", \"" + beID + "\", " + nextState + ")\n" );
	}
}
